--SET hive.execution.engine=mr;
set mapreduce.map.memory.mb=5120;
set mapreduce.reduce.memory.mb=5120;
set mapreduce.map.java.opts=-Xmx5120m -XX:-UseGCOverheadLimit;
set mapreduce.reduce.java.opts=-Xmx5120m -XX:-UseGCOverheadLimit;
set mapred.reduce.tasks=1799;

add jar s3://musically-bigdata/recommend_script/udf/log/recommend-offline-0.1.jar;
create temporary function user_cf_i2i_action_score as 'net.vickymedia.recommend.udf.user.i2i.UserCFI2iActionScoreReducer';

create EXTERNAL table if not exists user_cf_i2i_action_score
(
    from_id   				bigint,
    follow_list				string	comment "itemlist_format:  to_id,score,follow_rel,info\\;..."
)
partitioned by(dt string)
ROW FORMAT DELIMITED
FIELDS TERMINATED BY '\t'
STORED AS TEXTFILE
location 's3n://musically-bigdata/recommend_script/report/user_cf_i2i_action_score/'
TBLPROPERTIES (
    'auto.purge' = 'false'
);

insert overwrite table user_cf_i2i_action_score partition(dt=${hiveconf:today})
select
   from_id,
   user_cf_i2i_action_score(to_id, follow_rel, like_cnt, view_profile_cnt, loop_cnt) as follow_list
from
(
select
   from_id,
   to_id,
   follow_rel,
   like_cnt,
   comment_cnt,
   view_profile_cnt,
   loop_cnt,
   Row_Number() over (partition by from_id order by like_cnt+view_profile_cnt+loop_cnt desc) as rank
from
(
select
   from_id,
   to_id,
   max(follow_rel) as follow_rel,
   sum(like_cnt) as like_cnt,
   sum(like_cnt_7_days) as like_cnt_7_days,
   sum(comment_cnt) as comment_cnt,
   sum(comment_cnt_7_days) as comment_cnt_7_days,
   max(create_time) as create_time,
   max(update_time) as update_time,
   sum(view_profile_cnt) as view_profile_cnt,
   sum(view_profile_cnt_7_days) as view_profile_cnt_7_days,
   sum(loop_cnt) as loop_cnt,
   sum(loop_cnt_7_days) as loop_cnt_7_days
from
(
   select
      from_id,
      to_id,
      mutual as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      create_time,
      update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      user_follow
   union all
   select
      from_id,
      to_id,
      -1 as follow_rel,
      like_cnt,
      like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      user_like_rel_counter
   where dt = ${hiveconf:today}
   union all
   select
      from_id,
      to_id,
      -1 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      comment_cnt,
      comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      user_comment_rel
   where dt = ${hiveconf:today}
   union all
   select
      from_id,
      to_id,
      -1 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      search_cnt as search_cnt,
      search_cnt_7_days as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      user_search_rel_counter
   where dt = ${hiveconf:today}
   union all
   select
      from_id,
      to_id,
      -1 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      view_profile_cnt as view_profile_cnt,
      view_profile_cnt_7_days as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      view_profile_rel_counter
   where dt = ${hiveconf:today}
   union all
   select
      from_id,
      to_id,
      -1 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      loop_cnt as loop_cnt,
      loop_cnt_7_days as loop_cnt_7_days
   from
      user_loop_rel_counter
   where dt = ${hiveconf:today}
   and from_id is not null
   and to_id is not null
   and loop_cnt is not null
   and loop_cnt_7_days is not null
   union all
   select
      from_mus_id as from_id,
      to_mus_id as to_id,
      2 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      fb_musically_rel
   where dt=${hiveconf:yesterday}
   union all
   select
      fid as from_id,
      tid as to_id,
      3 as follow_rel,
      0 as like_cnt,
      0 as like_cnt_7_days,
      0 as comment_cnt,
      0 as comment_cnt_7_days,
      0 as create_time,
      0 as update_time,
      0 as search_cnt,
      0 as search_cnt_7_days,
      0 as view_profile_cnt,
      0 as view_profile_cnt_7_days,
      0 as loop_cnt,
      0 as loop_cnt_7_days
   from
      contact_direct_rel
   where dt = ${hiveconf:yesterday}
) a
group by from_id, to_id
) b
where from_id!=to_id
and follow_rel<=0
and (like_cnt>0 or comment_cnt>0)
) A
where rank<=250
group by from_id
;

--join
--(
--select
--   from_id as from_userid
--   , to_id as to_userid
--from
--user_follow
--where from_unixtime(cast(create_time/1000 as bigint), 'yyyyMMdd')>=20170101
--) B
--on (A.from_id=B.from_userid and A.to_id=B.to_userid)
